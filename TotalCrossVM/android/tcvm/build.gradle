apply plugin: 'com.android.library'

 repositories { 
     google()
     mavenCentral()
     maven {
         url "http://maven.totalcross.com/artifactory/build"
         allowInsecureProtocol true
         credentials {
             username = "$System.env.TC_REPO_USERNAME"
             password = "$System.env.TC_REPO_PASSWORD"
         }
     }
 }

android {
    compileSdkVersion 33
    ndkVersion "21.4.7075529"
    
    compileOptions {
    	// compile with Java 8 to allow using try-with-resources without raising the API level to 19
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 33
        versionCode 305419896
        versionName "!1.0!"
        multiDexEnabled true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        externalNativeBuild {
            cmake {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
                arguments "-DANDROID_USE_LEGACY_TOOLCHAIN_FILE=OFF", "-DANDROID_LIBS_DIR="+sourceSets.getByName("main").jniLibs.srcDirs[0]
                cppFlags "-std=c++14", "-DSK_GL"
                targets "tcvm"
            }
        }
    }

    packagingOptions {
        pickFirst "**/libskia.so"
    }
    
    signingConfigs {
        config {
            keyAlias 'tcandroidkey'
            keyPassword '@ndroidsw'
            storeFile file('../tcandroidkey.keystore')
            storePassword '@ndroid$w'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=Release"
                }
            }
        }
        debug {
            signingConfig signingConfigs.config
            externalNativeBuild {
                cmake {
                    arguments "-DDEBUG=1"
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "../../CMakeLists.txt"
        }
    }
}
